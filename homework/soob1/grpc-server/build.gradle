apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'application'

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.4'
	}
}

repositories {
	mavenCentral()
}

def grpcVersion = '1.56.1'

dependencies {
	implementation "io.grpc:grpc-netty:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation 'com.google.protobuf:protobuf-java:3.23.4'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.23.4'
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	manifest {
		attributes "Main-Class": "GrpcServerApplication"
	}
	from {
		configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

startScripts.enabled = false
